@Enumeration(DecodeMode [None Hadamard])
@Enumeration(RCAOrientation [None Rows Columns])
@Enumeration(SamplingMode [2X 4X])

@Table([name size elements]) DataKind
{
	[Int16          2 1]
	[Int16Complex   2 2]
	[Float32        4 1]
	[Float32Complex 4 2]
}

@Enumeration(EmissionKind [Sine SineAM Chirp])

@Table([type name]) SineParameters
{
	[F32 cycles   ]
	[F32 frequency]
}

@Table([type name]) SineAMParameters
{
	[F32 cycles   ]
	[F32 frequency]
	[U32 emissions]
}

@Table([type name]) ChirpParameters
{
	[F32 duration     ]
	[F32 min_frequency]
	[F32 max_frequency]
}

@MUnion(EmissionKind [
	SineParameters
	SineAMParameters
	ChirpParameters
]) Emission

@Table([name name_lower]) FilterKind
{
	[Kaiser       kaiser]
	[MatchedChirp matched_chirp]
}

@Table([type name]) KaiserFilterParameters
{
	[F32 cutoff_frequency]
	[F32 beta]
	[U32 length]
}

@Table([type name]) ChirpFilterParameters
{
	[F32 duration]
	[F32 min_frequency]
	[F32 max_frequency]
}

@MUnion(FilterKind [
	KaiserFilterParameters
	ChirpFilterParameters
]) Filter

@Table([name pretty_name fixed_transmits]) AcquisitionKind
{
	[FORCES         FORCES         1]
	[UFORCES        UFORCES        0]
	[HERCULES       HERCULES       1]
	[RCA_VLS        VLS            0]
	[RCA_TPW        TPW            0]
	[UHERCULES      UHERCULES      0]
	[RACES          RACES          1]
	[EPIC_FORCES    EPIC-FORCES    1]
	[EPIC_UFORCES   EPIC-UFORCES   0]
	[EPIC_UHERCULES EPIC-UHERCULES 0]
	[Flash          Flash          0]
	[HERO_PA        HERO-PA        0]
}

@Table([name]) InterpolationMode
{
	[Nearest]
	[Linear]
	[Cubic]
}

@Table([name type]) ParametersHead
{
	[xdc_transform                 M4]
	[xdc_element_pitch             V2]
	[raw_data_dimensions          UV2]
	[focal_vector                  V2]
	[transmit_receive_orientation U32]
	[sample_count                 U32]
	[channel_count                U32]
	[acquisition_count            U32]
	[das_shader_id                U32]
	[time_offset                  F32]
	[single_focus                  U8]
	[single_orientation            U8]
	[decode_mode                   U8]
	[sampling_mode                 U8]
}

@Table([name type]) ParametersUI
{
	[output_min_coordinate   V3]
	[output_max_coordinate   V3]
	[output_points          SV4]
	[sampling_frequency     F32]
	[demodulation_frequency F32]
	[speed_of_sound         F32]
	[f_number               F32]
	[off_axis_pos           F32]
	[interpolation_mode     U32]
	[coherency_weighting    U32]
	[beamform_plane         U32]
	[decimation_rate        U32]
}

@Table([name c_type m_type m_size]) ParametersExtra
{
	[emission_kind       BeamformerEmissionKind       uint32  1]
	[emission_parameters BeamformerEmissionParameters uint8  12]
}

// TODO(rnp): definable constants in meta code (or at least references to enum values)
@Table([name type elements]) ParametersSimple
{
	[channel_mapping               S16  256]
	[sparse_elements               S16  256]
	[transmit_receive_orientations  U8  256]
	[steering_angles               F32  256]
	[focal_depths                  F32  256]
	[compute_stages                S32   16]
	[compute_stage_parameters      S32   16]
	[compute_stages_count          U32    1]
	[data_kind                     S32    1]
}

@Expand(AcquisitionKind)   @Enumeration(AcquisitionKind   `$(name)`)
@Expand(DataKind)          @Enumeration(DataKind          `$(name)`)
@Expand(FilterKind)        @Enumeration(FilterKind        `$(name)`)
@Expand(InterpolationMode) @Enumeration(InterpolationMode `$(name)`)

@Emit
{
	`typedef union {`
	`	struct {`
		@Expand(SineParameters)   `		$(%type)$(|)$(name);`
	`	} sine;`
	`	struct {`
		@Expand(SineAMParameters) `		$(%type)$(|)$(name);`
	`	} sine_am;`
	`	struct {`
		@Expand(ChirpParameters)  `		$(%type)$(|)$(name);`
	`	} chirp;`
	`} BeamformerEmissionParameters;`
	``
	`typedef struct {`
	@Expand(ParametersHead)  `	$(%type)$(|)$(name);`
	@Expand(ParametersUI)    `	$(%type)$(|)$(name);`
	@Expand(ParametersExtra) `	$(c_type)$(|)$(name);`
	`} BeamformerParameters;`
	``
	`typedef struct {`
	@Expand(ParametersHead) `	$(%type)$(|)$(name);`
	`} BeamformerParametersHead;`
	``
	`typedef struct {`
	@Expand(ParametersUI) `	$(%type)$(|)$(name);`
	`} BeamformerUIParameters;`
	``
	`typedef struct {`
	@Expand(ParametersHead)   `	$(%type)$(|)$(name);`
	@Expand(ParametersUI)     `	$(%type)$(|)$(name);`
	@Expand(ParametersExtra)  `	$(c_type)$(|)$(name);`
	@Expand(ParametersSimple) `	$(%type)$(|)$(name)$(elements > 1 -> "[" elements "]");`
	`} BeamformerSimpleParameters;`
	``
	`typedef struct {`
	`	BeamformerFilterKind kind;`
	`	union {`
	`		struct {`
			@Expand(KaiserFilterParameters) `			$(%type)$(|)$(name);`
	`		} kaiser;`
	`		struct {`
			@Expand(ChirpFilterParameters)  `			$(%type)$(|)$(name);`
	`		} matched_chirp;`
	`	};`
	`	f32 sampling_frequency;`
	`	b16 complex;`
	`} BeamformerFilterParameters;`
	``
	`read_only global u8 beamformer_data_kind_element_size[] = {`
	@Expand(DataKind) `	$(size),`
	`};`
	``
	`read_only global u8 beamformer_data_kind_element_count[] = {`
	@Expand(DataKind) `	$(elements),`
	`};`
	``
	`read_only global u8 beamformer_data_kind_byte_size[] = {`
	@Expand(DataKind) `	$(size) * $(elements),`
	`};`
	``
	`read_only global u8 beamformer_acquisition_kind_has_fixed_transmits[] = {`
	@Expand(AcquisitionKind) `	$(fixed_transmits),`
	`};`
	``
	`read_only global s8 beamformer_acquisition_kind_strings[] = {`
	@Expand(AcquisitionKind) `	s8_comp("$(pretty_name)"),`
	`};`
	``
	`read_only global s8 beamformer_filter_kind_strings[] = {`
	@Expand(FilterKind) `	s8_comp("$(name)"),`
	`};`
	``
	`read_only global s8 beamformer_interpolation_mode_strings[] = {`
	@Expand(InterpolationMode) `	s8_comp("$(name)"),`
	`};`
}

@ShaderGroup Compute
{
	@Shader CudaDecode
	@Shader CudaHilbert

	@Shader(decode.glsl) Decode
	{
		@Enumeration(DataKind)
		@Enumeration(DecodeMode)
		@Flags([DilateOutput UseSharedMemory])

		@Bake
		{
			@BakeInt(DecodeMode           decode_mode           )
			@BakeInt(InputChannelStride   input_channel_stride  )
			@BakeInt(InputSampleStride    input_sample_stride   )
			@BakeInt(InputTransmitStride  input_transmit_stride )
			@BakeInt(OutputChannelStride  output_channel_stride )
			@BakeInt(OutputSampleStride   output_sample_stride  )
			@BakeInt(OutputTransmitStride output_transmit_stride)
			@BakeInt(ToProcess            to_process            )
			@BakeInt(TransmitCount        transmit_count        )
		}
	}

	@Shader(filter.glsl) Filter
	{
		@Enumeration(DataKind)
		@Flags([ComplexFilter OutputFloats])

		@Bake
		{
			@BakeInt(DecimationRate       decimation_rate       )
			@BakeInt(FilterLength         filter_length         )
			@BakeInt(InputChannelStride   input_channel_stride  )
			@BakeInt(InputSampleStride    input_sample_stride   )
			@BakeInt(InputTransmitStride  input_transmit_stride )
			@BakeInt(OutputChannelStride  output_channel_stride )
			@BakeInt(OutputSampleStride   output_sample_stride  )
			@BakeInt(OutputTransmitStride output_transmit_stride)
			@BakeInt(SampleCount          sample_count          )
			@BakeFloat(DemodulationFrequency demodulation_frequency)
			@BakeFloat(SamplingFrequency     sampling_frequency    )
		}

		@SubShader Demodulate
	}

	@Shader(das.glsl) DAS
	{
		@Enumeration(AcquisitionKind)
		@Enumeration(DataKind)
		@Enumeration(InterpolationMode)
		@Enumeration(RCAOrientation)
		@Flags([Fast Sparse CoherencyWeighting SingleFocus SingleOrientation])

		@Bake
		{
			@BakeInt(AcquisitionCount           acquisition_count           )
			@BakeInt(AcquisitionKind            acquisition_kind            )
			@BakeInt(ChannelCount               channel_count               )
			@BakeInt(InterpolationMode          interpolation_mode          )
			@BakeInt(SampleCount                sample_count                )
			@BakeInt(TransmitReceiveOrientation transmit_receive_orientation)

			@BakeFloat(DemodulationFrequency demodulation_frequency)
			@BakeFloat(FNumber               f_number              )
			@BakeFloat(FocusDepth            focus_depth           )
			@BakeFloat(SamplingFrequency     sampling_frequency    )
			@BakeFloat(SpeedOfSound          speed_of_sound        )
			@BakeFloat(TimeOffset            time_offset           )
			@BakeFloat(TransmitAngle         transmit_angle        )
		}
	}

	@Shader(min_max.glsl) MinMax
	@Shader(sum.glsl) Sum
	@Shader(high_pass_filter.glsl) HighPassFilter
	{
		@Enumeration(DataKind)
	}
}

@ShaderGroup Render
{
	@Shader(render_3d.frag.glsl) Render3D
}

/////////////////
// NOTE: Library

@Emit(CLibrary)
{
	`typedef union {`
	`	struct {`
		@Expand(SineParameters)   `		$(%type)$(|)$(name);`
	`	} sine;`
	`	struct {`
		@Expand(SineAMParameters) `		$(%type)$(|)$(name);`
	`	} sine_am;`
	`	struct {`
		@Expand(ChirpParameters)  `		$(%type)$(|)$(name);`
	`	} chirp;`
	`} BeamformerEmissionParameters;`
	``
	`typedef struct {`
	@Expand(ParametersHead)   `	$(%type)$(|)$(name)$(#type > 1 -> "[" #type "]");`
	@Expand(ParametersUI)     `	$(%type)$(|)$(name)$(#type > 1 -> "[" #type "]");`
	@Expand(ParametersExtra)  `	$(c_type)$(|)$(name);`
	@Expand(ParametersSimple) `	$(%type)$(|)$(name)$(elements > 1 -> "[" elements "]");`
	`} BeamformerSimpleParameters;`
	``
	`typedef struct {`
	@Expand(ParametersUI)     `	$(%type)$(|)$(name)$(#type > 1 -> "[" #type "]");`
	`} BeamformerUIParameters;`
	``
	`typedef struct {`
	@Expand(ParametersHead)   `	$(%type)$(|)$(name)$(#type > 1 -> "[" #type "]");`
	`} BeamformerParametersHead;`
	``
	`typedef struct {`
	@Expand(ParametersHead)   `	$(%type)$(|)$(name)$(#type > 1 -> "[" #type "]");`
	@Expand(ParametersUI)     `	$(%type)$(|)$(name)$(#type > 1 -> "[" #type "]");`
	@Expand(ParametersExtra)  `	$(c_type)$(|)$(name);`
	`} BeamformerParameters;`
	``
}

////////////////
// NOTE: MATLAB

@Emit(MATLAB) Parameters
{
	`classdef OGLBeamformerParameters`
	`	properties`
	@Expand(ParametersHead)  `		$(name)(1,$(#type))$(|)$(%type)`
	`		% UI Parameters`
	@Expand(ParametersUI)    `		$(name)(1,$(#type))$(|)$(%type)`
	@Expand(ParametersExtra) `		$(name)(1,$(m_size))$(|)$(m_type)`
	`	end`
	`end`
}

@Emit(MATLAB) ParametersHead
{
	`classdef OGLBeamformerParametersHead`
	`	properties`
	@Expand(ParametersHead) `		$(name)(1,$(#type))$(|)$(%type)`
	`	end`
	`end`
}

@Emit(MATLAB) ParametersUI
{
	`classdef OGLBeamformerParametersUI`
	`	properties`
	@Expand(ParametersUI) `		$(name)(1,$(#type))$(|)$(%type)`
	`	end`
	`end`
}

@Emit(MATLAB) SimpleParameters
{
	`classdef OGLBeamformerSimpleParameters`
	`	properties`
	@Expand(ParametersHead)   `		$(name)(1,$(#type)) $(%type)`
	@Expand(ParametersUI)     `		$(name)(1,$(#type)) $(%type)`
	@Expand(ParametersExtra)  `		$(name)(1,$(m_size))$(|)$(m_type)`
	@Expand(ParametersSimple) `		$(name)(1,$(elements)) $(%type)`
	`	end`
	`end`
}
